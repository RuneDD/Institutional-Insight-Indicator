// Institutional Insight Indicator
// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© RuneDD

//@version=6
indicator("Institutional Insight Indicator [Enhanced]", overlay=true, shorttitle="III [E]")

// ------------------ Parameters ------------------
// General
multiplier = input.float(2.5, title="Volume Multiplier")
length = input.int(50, title="Length of Moving Average")
buffer = input.float(0.005, title="Buffer for Swing Highs/Lows", minval=0.001, maxval=0.05, step=0.001)

// SMA
smaLength2 = input.int(200, title="SMA Length for Trend")
zoneLength = input.int(50, title="Length for Supply/Demand Zones")

// RSI
rsiPeriod = input.int(14, title="RSI Period")
rsiValue = ta.rsi(close, rsiPeriod)

// ------------------ Calculations ------------------
volatility = ta.stdev(close, 20)
dynamicMultiplier = multiplier * volatility
avgVolume = ta.sma(volume, length)
trendSMA2 = ta.sma(close, smaLength2)

// Swings
swingHigh = close > (1 + buffer) * ta.highest(close[1], zoneLength) and close[1] > close[2]
swingLow = close < (1 - buffer) * ta.lowest(close[1], zoneLength) and close[1] < close[2]

// Fibonacci
mostRecentSwingHigh = ta.valuewhen(swingHigh, close, 0)
mostRecentSwingLow = ta.valuewhen(swingLow, close, 0)
fib382 = mostRecentSwingLow + 0.382 * (mostRecentSwingHigh - mostRecentSwingLow)
fib500 = mostRecentSwingLow + 0.500 * (mostRecentSwingHigh - mostRecentSwingLow)
fib618 = mostRecentSwingLow + 0.618 * (mostRecentSwingHigh - mostRecentSwingLow)

// Pressures
buyingPressure = close > trendSMA2 and close > open and volume > avgVolume * dynamicMultiplier and close[1] < close and rsiValue < 30
sellingPressure = close < trendSMA2 and close < open and volume > avgVolume * dynamicMultiplier and close[1] > close and rsiValue > 70

// ------------------ Visualization ------------------
// Bar Colors
barcolor(buyingPressure ? color.new(color.green, 90) : sellingPressure ? color.new(color.red, 90) : na)

// SMA Plot
plot(trendSMA2, title="Trend SMA", color=color.purple, linewidth=2)

// Swing Indicators
plotshape(series=swingHigh, title="Swing High", location=location.belowbar, color=color.green, style=shape.labelup, text="High")
plotshape(series=swingLow, title="Swing Low", location=location.abovebar, color=color.red, style=shape.labeldown, text="Low")

// Fibonacci Levels
plot(fib382, title="Fibonacci 38.2%", color=color.new(color.orange, 60), linewidth=2)
plot(fib500, title="Fibonacci 50%", color=color.new(color.blue, 60), linewidth=1)
plot(fib618, title="Fibonacci 61.8%", color=color.new(color.orange, 60), linewidth=2)

// High Volume Areas
highVolume = volume > avgVolume * dynamicMultiplier
bgcolor(highVolume ? color.new(color.blue, 95) : na)

// ------------------ Alerts ------------------
alertcondition(buyingPressure, title="Buying Pressure Alert", message="Buying Pressure Detected!")
alertcondition(sellingPressure, title="Selling Pressure Alert", message="Selling Pressure Detected!")

