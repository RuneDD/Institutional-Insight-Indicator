// Advanced Institutional Insight Indicator
// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© RuneDD
//@version=5
indicator("Advanced Institutional Insight Indicator", overlay=true)

// ------------------ Parameters ------------------
// General Parameters
multiplier = input.float(2.5, title="Volume Multiplier")
length = input.int(50, title="Length of Moving Average")
buffer = input.float(0.005, title="Buffer for Swing Highs/Lows", minval=0.001, maxval=0.05, step=0.001)

// SMA Parameters
smaLength2 = input.int(200, title="SMA Length for Trend")

// Zone Length
zoneLength = input.int(50, title="Length for Supply/Demand Zones")

// RSI Parameters
rsiPeriod = input.int(14, title="RSI Period")

// ------------------ Calculations ------------------
// Calculate RSI
rsiValue = ta.rsi(close, rsiPeriod)

// Calculate volatility and dynamic multiplier
volatility = ta.stdev(close, 20)
dynamicMultiplier = multiplier * volatility

// SMA for trend and average volume
avgVolume = ta.sma(volume, length)
trendSMA2 = ta.sma(close, smaLength2)

// Identify market swings
swingHigh = close > (1 + buffer) * ta.highest(close[1], zoneLength) and close[1] > close[2]
swingLow = close < (1 - buffer) * ta.lowest(close[1], zoneLength) and close[1] < close[2]

// Get the most recent swings for Fibonacci calculations
mostRecentSwingHigh = ta.valuewhen(swingHigh, close, 0)
mostRecentSwingLow = ta.valuewhen(swingLow, close, 0)

// Identify buying and selling pressures
buyingPressure = close > trendSMA2 and close > open and volume > avgVolume * dynamicMultiplier and close[1] < close and rsiValue < 30
sellingPressure = close < trendSMA2 and close < open and volume > avgVolume * dynamicMultiplier and close[1] > close and rsiValue > 70

// Calculate Fibonacci levels
fib382 = mostRecentSwingLow + 0.382 * (mostRecentSwingHigh - mostRecentSwingLow)
fib500 = mostRecentSwingLow + 0.500 * (mostRecentSwingHigh - mostRecentSwingLow)
fib618 = mostRecentSwingLow + 0.618 * (mostRecentSwingHigh - mostRecentSwingLow)

// ------------------ Visualization ------------------
// Highlight bars based on buying and selling pressure
barcolor(buyingPressure ? color.green : sellingPressure ? color.red : na)

// Plot SMA, swings and Fibonacci levels
plot(trendSMA2, title="Trend SMA", color=color.purple, linewidth=1)
plotshape(series=swingHigh, title="Swing High", location=location.belowbar, color=color.new(color.green, 60), style=shape.triangleup, size=size.small)
plotshape(series=swingLow, title="Swing Low", location=location.abovebar, color=color.new(color.red, 60), style=shape.triangledown, size=size.small)
plot(fib382, title="Fibonacci 38.2%", color=color.new(color.maroon, 50), linewidth=1)
plot(fib500, title="Fibonacci 50%", color=color.new(#214725, 50), linewidth=1)
plot(fib618, title="Fibonacci 61.8%", color=color.new(color.maroon, 50), linewidth=1)

// Highlight high volume areas with background color
highVolume = volume > avgVolume * dynamicMultiplier
bgcolor(highVolume ? color.new(color.blue, 90) : na)

// ------------------ Alerts ------------------
alertcondition(buyingPressure, title="Buying Pressure Alert", message="Buying Pressure Detected!")
alertcondition(sellingPressure, title="Selling Pressure Alert", message="Selling Pressure Detected!")
